buildscript {
  dependencies {
    classpath deps.triplet
  }
}

plugins {
  id 'com.android.application'
  id 'com.github.triplet.play'
}

android {
  defaultConfig {
    applicationId "de.hannesstruss.alter"

    buildConfigField "String", "BUGSNAG_API_KEY", "\"${getProperty('alter.bugsnag_key')}\""
  }
  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'hunter2'
      keyAlias 'key0'
      keyPassword 'hunter2'
    }

    if (file('upload.keystore').exists()) {
      upload {
        storeFile file('upload.keystore')
        storePassword getProperty('alter.upload_store_password')
        keyAlias 'key0'
        keyPassword getProperty('alter.upload_key_password')
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix = ".dev"
      versionNameSuffix = "-dev"
      minifyEnabled false
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled false
      if (file('upload.keystore').exists()) {
        signingConfig signingConfigs.upload
      } else {
        signingConfig signingConfigs.debug
      }
    }
  }

  packagingOptions {
    pickFirst "META-INF/atomicfu.kotlin_module"
    pickFirst "META-INF/kotlinx-coroutines-core.kotlin_module"
  }
}

dependencies {
  implementation project(':modules:app:navigation')
  implementation project(':modules:app:theme')

  implementation project(':modules:app:features:common')
  implementation project(':modules:app:features:feature-list')
  implementation project(':modules:app:features:feature-baby-detail')
  implementation project(':modules:app:features:feature-edit-baby')

  implementation project(':modules:app:db-android')

  implementation project(':modules:aux:flowbinding')
  implementation project(':modules:aux:statemachine')
  implementation project(':modules:aux:statemachine-aac')

  implementation deps.android.support.appCompat
  implementation deps.android.support.constraintLayout
  implementation deps.android.arch.lifecycle.extensions
  implementation deps.android.arch.navigation.fragment
  implementation deps.android.arch.navigation.ui

  implementation deps.bugsnag.sdk

  testImplementation deps.junit
}

play {
  if (file('publishkey.json').exists()) {
    serviceAccountCredentials = file('publishkey.json')
  } else {
    enabled = false
  }
}
