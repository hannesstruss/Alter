buildscript {
  ext.buildConfig = [
      'compileSdk': 28,
      'minSdk': 26,
      'targetSdk': 28,

      'version': [
          'major': 0,
          'minor': 3,
          'patch': 0,
          'build': 1,
      ],
  ]

  def p = "git rev-list --count HEAD".execute([], rootDir)
  if (p.waitFor() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }
  int numberOfCommits = p.text.trim().toInteger()

  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
  if (System.getenv("GITHUB_WORKFLOW") == null) {
    ext.buildConfig.version['code'] = 1
  } else {
    int lastVersionCodeBeforeUsingCommits = 30001
    ext.buildConfig.version['code'] = numberOfCommits + lastVersionCodeBeforeUsingCommits
  }


  ext.versions = [
      'supportLibrary': '1.0.2',
      'kotlin': [
          'lang': '1.3.71',
          'coroutines': '1.3.5'
      ],
      'dagger': '2.27',
      'arch': [
          'navigation': '2.0.0',
          'lifecycle': '2.0.0'
      ],
      'playservices': [
          'fit': '16.0.1',
          'auth': '16.0.1'
      ],
      'rx': [
          'binding': '3.0.0-alpha2'
      ],
      'sqldelight': '1.3.0'
  ]

  ext.deps = [
      'kotlin': [
          'stdlib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin.lang}",
              'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.lang}",
          ],
          'coroutines': [
              'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
              'rx2': "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${versions.kotlin.coroutines}",
              'playservices': "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:${versions.kotlin.coroutines}",
              'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin.coroutines}",
          ],
          'gradle': [
              'multiplatform': 'org.jetbrains.kotlin.multiplatform',
          ],
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin.lang}",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin.lang}",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin.lang}",
          ]
      ],
      'android': [
          'support': [
              'appCompat': "androidx.appcompat:appcompat:${versions.supportLibrary}",
              'fragment': "androidx.fragment:fragment-ktx:1.1.0-alpha09",
              'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
          ],
          'material': [
              'material': "com.google.android.material:material:1.1.0-alpha09"
          ],
          'arch': [
              'navigation': [
                  'fragment': "androidx.navigation:navigation-fragment-ktx:${versions.arch.navigation}",
                  'ui': "androidx.navigation:navigation-ui-ktx:${versions.arch.navigation}",
                  'safeargs_plugin': "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.arch.navigation}"
              ],
              'lifecycle': [
                  'viewmodel': "androidx.lifecycle:lifecycle-viewmodel:${versions.arch.lifecycle}",
                  'extensions': "androidx.lifecycle:lifecycle-extensions:${versions.arch.lifecycle}"
              ],
              'work': 'androidx.work:work-runtime-ktx:2.0.0'
          ],
          'ktx': [
              'core': 'androidx.core:core-ktx:1.0.1',
              'sqlite': 'androidx.sqlite:sqlite-ktx:2.0.0'
          ]
      ],
      'okio': 'com.squareup.okio:okio:2.2.2',
      'bugsnag': [
          'sdk': 'com.bugsnag:bugsnag-android:4.19.0'
      ],
      'gson': 'com.google.code.gson:gson:2.8.5',
      'timber': 'com.jakewharton.timber:timber:4.7.1',
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}"
      ],
      'processphoenix': 'com.jakewharton:process-phoenix:2.0.0',
      'playservices': [
          'fit': "com.google.android.gms:play-services-fitness:${versions.playservices.fit}",
          'auth': "com.google.android.gms:play-services-auth:${versions.playservices.auth}"
      ],
      's3': 'com.amazonaws:aws-android-sdk-s3:2.13.4',
      'sqldelight': [
          'runtime': "com.squareup.sqldelight:runtime:${versions.sqldelight}",
          'gradle': "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}",
          'coroutines': "com.squareup.sqldelight:coroutines-extensions:${versions.sqldelight}",
          'android_driver': "com.squareup.sqldelight:android-driver:${versions.sqldelight}",
          'ios_driver': "com.squareup.sqldelight:native-driver-iosx64:${versions.sqldelight}",
      ],
      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.43',
      'triplet': 'com.github.triplet.gradle:play-publisher:2.3.0'
  ]

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url("https://oss.sonatype.org/content/repositories/snapshots") }
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.lang}"
    classpath deps.triplet
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.21.0"
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

subprojects { subproject ->
  buildscript {
    repositories {
      google()
      mavenCentral()
      jcenter()
      maven { url("https://oss.sonatype.org/content/repositories/snapshots") }
    }
  }

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url("https://oss.sonatype.org/content/repositories/snapshots") }
  }

  plugins.withId("com.android.application") {
    subproject.apply from: rootProject.file("setup_android_module.gradle")
  }

  plugins.withId("com.android.library") {
    subproject.apply from: rootProject.file("setup_android_module.gradle")
  }

  plugins.withId("kotlin") {
    subproject.apply from: rootProject.file("setup_kotlin_module.gradle")
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
      freeCompilerArgs = [
          "-XXLanguage:+InlineClasses",
          '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
          '-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi',
          '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
      ]
    }
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all Kotlin coroutines artifacts to use the same version.
        if (details.requested.group == 'org.jetbrains.kotlinx'
            && details.requested.name.startsWith('kotlinx-coroutines')) {
          details.useVersion versions.kotlin.coroutines
        }
      }
    }
  }
}

subprojects { subProject ->
  subProject.configurations.all { configuration ->
    // Workaround for kapt bug with MPP dependencies
    // https://youtrack.jetbrains.com/issue/KT-31641
    // https://youtrack.jetbrains.com/issue/KT-33206
    if (name.contains('kapt')) {
      attributes.attribute(Usage.USAGE_ATTRIBUTE, subProject.objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

def propOrEmpty(String name) {
  return hasProperty(name) ? getProperty(name) : ''
}
